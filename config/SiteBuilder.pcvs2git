// -*- Pike -*-
//
// Configuration file for generating the Git repository
// for SiteBuilder from its CVS repositories.
//
// 2010-01-04 Henrik Grubbström

inherit "BranchDir.pcvs2git";

/* Notes about history changes:
 *
 * 97-09-11	Punisher module added.
 * 98-02-13	Punisher renamed SiteBuilder.
 * 98-10-20	NewSiteBuilder (aka 1.3) split.
 * 99-10-20	Removes dir level for weblayout.
 * 98-11-10	AC/1.1 split.
 * 98-12-07	Roxen Platform + IntraSeek.
 * 99-09-08	NewSiteBuilder renamed SiteBuilder/1.3-stable.
 * 99-09-08	SiteBuilder/1.4 and AC/1.4 split (aka 2.0).
 * 99-10-22	XSLT module added to SiteBuilder/1.4 (aka 2.0).
 * 2000-01-30	Javascript Support module added to SiteBuilder/1.4 (aka 2.0).
 * 2000-03-10	Weblayout/2.0 and Navigation/2.0 for SiteBuilder/2.0.
 * 2000-04-25	SiteBuilder/2.1 split.
 * 2000-06-13	XSLT/2.1 split.
 * 2000-08-28	IntraSeek/2.1 split.
 * 2000-09-30	SiteBuilder/2.2 split.
 * 2001-09-25	SiteBuilder/2.3 split.
 * 2002-01-14	SiteBuilder/2.4 split.
 * 2002-08-19	SiteBuilder/3.3 split.
 * 2002-11-02	SiteBuilder/3.4 split.
 * 2003-06-02	SiteBuilder/3.5 split (other modules now integrated).
 * 2003-12-16	SiteBuilder/3.5 renamed 4.0.
 * 2005-12-20	SiteBuilder/4.5 split.
 * 2006-10-30	SiteBuilder/4.6 split.
 * 2007-04-26	SiteBuilder/4.6 renamed 5.0.
 * 2010-11-29	SiteBuilder/5.1 split.
 * 2011-01-18	SiteBuilder/5.2 split.
 */


void describe_rcs_file(GitRepository git, RCSFile rcs_file, string|void path)
{
  werror("Path: %O(%O) (master_branch: %O)\n"
	 "Head: %O branch: %O\n",
	 path||"", rcs_file->rcs_file_name, git->master_branch,
	 rcs_file->head, rcs_file->branch);
  array(RCSFile.Revision) revisions = values(rcs_file->revisions);
  sort(revisions->time->unix_time(), revisions);
  foreach(revisions, RCSFile.Revision rev) {
    if (rev->ancestor) {
      werror("  %s(%s):%s\t==> %s%s\n",
	     rev->revision, ctime(rev->time->unix_time()) - "\n",
	     rev->state, rev->ancestor,
	     (rev->revision_flags & GIT_REVISION_MERGE)?" (MERGE)":"");
    } else {
      werror("  %s(%s):%s\t==> NULL\n",
	     rev->revision, ctime(rev->time->unix_time()) - "\n", rev->state);
    }
  }
}

string real_master_branch;

void set_master(GitRepository git, mapping state, string master,
		string|void module)
{
  state->orig_master = git->master_branch;
  string orig_master = master;
  if (module == "ac") {
    // Remap some early versions of ac.
    switch(master) {
    case "1.0": master = "1.0"; break;
    case "1.1": master = "1.3"; break;
    case "1.4": master = "2.0"; break;
    }
  }
  git->set_master_branch(master);
  real_master_branch = master;
#if 0
  if (module) {
    // This is a submodule.
    GitRepository.GitCommit sb_master = git->git_refs[git->remote + master];
    // Note: We can't use set_master_branch() here, since
    //       then the module branches won't tangle.
    git->master_branch = "roxen-modules/" + module + "/" + orig_master;
    module = git->remote + git->master_branch;
    GitRepository.GitCommit module_branch = git->ref_factory(module);
    git->heads |= module_branch->is_leaf;
    // Make sure that the module is part of the corresponding sitebuilder.
    sb_master->hook_parent(module_branch);
  }
#endif
}

array(string|array(string)) enter_directory(GitRepository git, string path,
					    array(string) files,
					    GitFlags flags, mapping state)
{
  m_delete(state, "orig_master");
  m_delete(state, "flush");
  if (state->strip_next_dir > 0) {
    state->strip_next_dir--;
    path = combine_path(path, "..");
  }
  switch(path) {
  case "":
    // Make sure that the directories are scanned in a reasonable order,
    // to ensure that the code in filter_commits() works.

    // This repository contains some custom handlers.
    // FIXME: Skipped for now.
    files -= ({ "sitebuilder_hack" });

    // Perform some reordering.
    array(string) new_files =
      ({ "ac", "weblayout", "navigation", "sitebuilder", "XSLT",
	 "navigation_1.3", "navigation_2.0",
	 "navigation_2.1", "navigation_2.2",
	 "navigation_2.3", "navigation_2.4",
	 "navigation_3.3", "navigation_3.4",
	 "weblayout_1.3", "weblayout_2.0",
	 "weblayout_2.1", "weblayout_2.2",
	 "weblayout_2.3", "weblayout_2.4",
	 "weblayout_3.3", "weblayout_3.4",
	 "javascript_support", "javascript_support_2.1" }) & files;

    files = new_files + (files - new_files);
    break;
  case "XSLT":
    path = "xsltransform";
    // FALL_THROUGH
  case "SiteBuilder":
    path = lower_case(path);
    // FALL_THROUGH
  case "ac":
    state->module = path;
    path = "";
    break;
  case "weblayout":
  case "navigation":
    set_master(git, state, "1.0", path);
    path = "modules/" + path;
    break;
  case "weblayout_1.3":	// Note: Fake!
  case "weblayout_2.0":
  case "weblayout_2.1":	// Note: Fake!
  case "weblayout_2.2":
  case "weblayout_2.3":
  case "weblayout_2.4":
  case "weblayout_3.3":
  case "weblayout_3.4":
    // Only keep the modules directory, and strip it from the path.
    files = filter(files, `==, "modules");
    state->strip_next_dir++;
    // FALL_THROUGH
  case "navigation_1.3":
  case "navigation_2.0":
  case "navigation_2.1":
  case "navigation_2.2":
  case "navigation_2.3":
  case "navigation_2.4":
  case "navigation_3.3":
  case "navigation_3.4":
    set_master(git, state, (path/"_")[-1], (path/"_")[0]);
    path = "modules/" + (path/"_")[0];
    break;
  case "javascript_support":
    set_master(git, state, "2.0", "javascript_support");
    path = "modules/javascript_support";
    break;
  case "javascript_support_2.1":
    set_master(git, state, "2.1", "javascript_support");
    path = "modules/javascript_support";
    break;
  case "sitebuilder":
    // aka punisher.
    set_master(git, state, "1.0");
    path = "";
    break;
  case "sitebuilder_hack":
    // "Repository for special extensions to SiteBuilder."
    // FIXME: Not yet supported.
    set_master(git, state, "sitebuilder/hack");
    path = "";
    break;
  case "1.3-stable":
    path = "1.3";
  case "1.0":case "1.1":case "1.2":case "1.3":case "1.4":
  case "2.0":case "2.1":case "2.2":case "2.3":case "2.4":
  case "3.3":case "3.4":case "4.0":case "4.5":
  case "5.0":case "5.1":case "5.2":
    if(state->module == "sitebuilder") {
      werror("\nSiteBuilder/%s\n", path);
      set_master(git, state, path);
      path = "";
    } else {
      werror("\n%s/%s\n", state->module, path);
      set_master(git, state, path, state->module);
      path = "modules/" + state->module;
    }
    break;
  }
  // We believe that the automatically generated .gitignores
  // work better than the handcoded.
  files -= ({ ".gitignore,v" });
  return ({ path, files });
}

void leave_directory(GitRepository git, string path, array(string) files,
		     GitFlags flags, mapping state)
{
  if (!zero_type(state->orig_master)) {
    // Propagate deletions for permanently deleted files.
    // This is needed to keep filter_commits() happy further down.
    git_progress(git_flags, "\n");
    switch(real_master_branch) {
    case "1.3":
      branch_dependancy(git, "1.0", "1.3", "98.10.13.19.37.21");
      break;
    case "2.0":
      branch_dependancy(git, "1.3", "2.0", "99.09.04.16.23.30");
      break;
    case "2.1":
      branch_dependancy(git, "2.0", "2.1", "2000.04.25.14.24.00");
      break;
    case "2.2":
      branch_dependancy(git, "2.1", "2.2", "2000.09.30.00.00.00");
      break;
    case "2.3":
      branch_dependancy(git, "2.2", "2.3", "2001.09.25.00.00.00");
      break;
    case "2.4":
      branch_dependancy(git, "2.3", "2.4", "2002.01.14.00.00.00");
      break;
    case "3.3":
      branch_dependancy(git, "2.4", "3.3", "2002.08.19.00.00.00");
      break;
    case "3.4":
      branch_dependancy(git, "3.3", "3.4", "2002.11.02.00.00.00");
      break;
    case "4.0":
      branch_dependancy(git, "3.4", "4.0", "2003.06.02.16.07.13");
      break;
    case "4.5":
      branch_dependancy(git, "4.0", "4.5", "2004.08.18.11.48.21");
      break;
    case "5.0":
      branch_dependancy(git, "4.5", "5.0", "2006.10.30.00.00.00");
      break;
    case "5.1":
      branch_dependancy(git, "5.0", "5.1", "2010.11.29.00.00.00");
      break;
    case "5.2":
      branch_dependancy(git, "5.1", "5.2", "2011.01.18.10.14.21");
      break;
    }

    // Unify the commits, to reduce confusion when other
    // branches are added.
    if (state->flush) {
      git_progress(flags, "\nFlushing branch %s...\n", git->master_branch);
      git->flush(flags);
      git_progress(flags, "\nContinuing import...\n");
    }
    git->master_branch = state->orig_master;
    if (state->orig_master) {
      git->latest_master_branch = git->remote + state->orig_master;
    }
    if (state->orig_fuzz) {
      git->fuzz = state->orig_fuzz;
    }
  }
}

void sticky_revision(RCSFile rcs_file, string rcs_time, string author,
		     string revert_rev, string branch_rev, string patch_rev,
		     array(string) tags, string|void message)
{
  if (rcs_file->tags[tags[0]] == revert_rev) {
    Calendar.TimeRange time =
      Calendar.ISO.parse("%y.%M.%D.%h.%m.%s %z", rcs_time + " UTC");
    rcs_file->append_revision(revert_rev, branch_rev, time,
			      author, message || "Sticky revision.\n",
			      patch_rev, "Exp");
    foreach(tags, string tag) {
      string rev = rcs_file->tags[tag];
      if (!rev) continue;
      if (rev == revert_rev) {
	rcs_file->tags[tag] = patch_rev;
      }
    }
  }
}

void split_branch(RCSFile rcs_file, string branch, string rcs_time)
{
  if (rcs_file->tags[branch]) return;
  string rev = find_revision(rcs_file, UNDEFINED, rcs_time);
  if (!rev) {
    // File was created after the branch was split off.
    // Ensure that the file isn't merged into the branch.
    kill_branch(rcs_file, branch);
    return;
  }
  string rev2 = find_revision(rcs_file, "", rcs_time);
  if (rev2 && (rcs_file->revisions[rev2]->time >=
	       rcs_file->revisions[rev]->time)) {
    rev = rev2;
  }
  add_branch(rcs_file, branch, rev);
}

void repair_rcs_file(GitRepository git, string path, RCSFile rcs_file,
		     GitFlags flags)
{
  switch(path) {
  case "modules/ac/acadm.pike":
    // Sticky tags for forsmark.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.108", "1.109", "1.109.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.108", "1.109", "1.109.4.1", ({ "forsmark_20000214", }));
      sticky_revision(rcs_file, "2000.02.20.01.10.01", "mast",
		      "1.108", "1.109", "1.109.6.1", ({ "forsmark_20000218", }));
    }
    // This file was moved in the AC/1.1 split.
    // CVSROOT/modules 1.50.
    // 98.11.10.11.34.00
    rename_revision(rcs_file, replace(path, "modules/ac/",
				      "modules/ac/modules/"), path,
		    append_revision(rcs_file, UNDEFINED, UNDEFINED,
				    "98.11.10.11.34.00",
				    "mast", "Börja på ny version av AC.\n"));
    // FALL_THROUGH
  case "modules/ac/modules/acadm.pike":
    // Set a name on the anonymous branch by mast 1997-10-30.
    if (rcs_file->revisions["1.21.2.1"]) {
      // rcs_file->revisions["1.21.2.1"]->ancestor = "1.21";
      rcs_file->tags["mast/ac_hack_1997-10-30"] = "1.21.2";
      rcs_file->branches["1.21.2"] = "mast/ac_hack_1997-10-30";
      rcs_file->branch_heads["1.21.2"] = "1.21.2.1";
      rcs_file->revisions["1.21.2.1"]->merges = ({ "1.22" });
    }
    break;

  case "modules/ac/acauthmodule.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.32.2.2"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.32.2.2";
    }
    if (git->master_branch >= "5.0") {
      // It seems this tag mistakenly was put on a revision
      // on the master branch for this file.
      m_delete(rcs_file->tags, "sitebuilder_5_0_workflow_merge1");
    }
    // FALL_THROUGH
  case "modules/ac/aclib.pike":
    // These were moved again in the AC/1.1 split.
    // CVSROOT/modules 1.50.
    // 98.11.10.11.34.00
    rename_revision(rcs_file,
		    replace(path, "modules/ac/",
			    "modules/ac/modules/"), path,
		    append_revision(rcs_file, UNDEFINED, UNDEFINED,
				    "98.11.10.11.34.00",
				    "mast", "Börja på ny version av AC.\n"));
    path = replace(path, "modules/ac/", "modules/ac/modules/");
    // FALL_THROUGH
  case "modules/ac/modules/acauthmodule.pike":
  case "modules/ac/modules/aclib.pike":
    // Set a name on the anonymous branch by mast 1997-10-30.
    if (rcs_file->revisions["1.18.2.1"]) {
      // rcs_file->revisions["1.18.2.1"]->ancestor = "1.18";
      rcs_file->tags["mast/ac_hack_1997-10-30"] = "1.18.2";
      rcs_file->branches["1.18.2"] = "mast/ac_hack_1997-10-30";
      rcs_file->branch_heads["1.18.2"] = "1.18.2.1";
      rcs_file->revisions["1.18.2.1"]->merges = ({ "1.19" });
    }

    // These were moved from the module root directory
    // to the modules directory.
    // 98.10.20.03.16.01
    rename_revision(rcs_file,
		    replace(path, "modules/ac/modules/",
			    "modules/ac/"), path,
		    find_revision(rcs_file, UNDEFINED, "98.10.20.03.16.30"));
    break;


    // SiteBuilder/1.3
  case "doc/hackers_overview.html":
    if (rcs_file->revisions["1.1"]->log == "File states take two.\n") {
      append_revision(rcs_file, "1.3", UNDEFINED, "98.10.17.07.53.55", "mast",
		      "Imported from the old repository.\n", "dead");
    }
    break;

  case "manager/content_editor.pike":
  case "manager/workarea_filesystem.pike":
    // These were deleted in cvs when they were moved to
    // modules/manager/...
    // 98.10.19.19.09.18
    append_revision(rcs_file, "1.3", UNDEFINED, "98.10.19.19.09.18", "mast",
		    "Uncomplete work on the init stuff.\n", "dead");
    break;

  case "modules/weblayout/weblayout.pike":
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.20.01.10.01", "mast",
		      "1.21", "1.23", "1.23.6.1", ({ "forsmark_20000218", }));
    }
    // FALL_THROUGH
  case "modules/weblayout/roxen-20-suggestions.txt":
    // These were moved in the SiteBuilder/1.3 split.
    // 98.10.20.06.16.29
    rename_revision(rcs_file, replace(path, "modules/weblayout/",
				      "modules/weblayout/modules/"), path,
		    append_revision(rcs_file, UNDEFINED, UNDEFINED,
				    "98.10.20.06.16.29",
				    "mast", "Moved the libraries to the modules directory.\n"));
    break;

  case "modules/weblayout/docs/examples.html":
  case "modules/weblayout/docs/reference.html":
  case "modules/weblayout/docs/whitepaper.html":
  case "modules/weblayout/private/roxen-20-suggestions.txt":
  case "modules/weblayout/private/saljspec.txt":
  case "modules/weblayout/private/spec.txt":
    // These were "deleted" in the SiteBuilder/1.3 split.
    // 98.10.20.06.16.29
    append_revision(rcs_file, "1.3", UNDEFINED, "98.10.20.06.16.29", "mast",
		    "Moved the libraries to the modules directory.\n", "dead");
    break;

    // AC/1.1
  case "modules/ac/acauth_httpbasic.pike":
    if ((git->master_branch >= "4.0") &&
	(rcs_file->tags["ad_work_sync"] == "1.21")) {
      // This file was backdated one revision in this tag.
      rcs_file->tags["ad_work_sync"] = "1.22";
    }
    // FALL_THROUGH
  case "modules/ac/acimport_acdb.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.23.2.1"] &&
	(path == "modules/ac/acimport_acdb.pike")) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.23.2.1";
    }
    // FALL_THROUGH
  case "modules/ac/acimport_osuser.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.68.2.1"] &&
	(path == "modules/ac/acimport_osuser.pike")) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.68.2.1";
    } else if ((git->master_branch >= "4.0") && rcs_file->revisions["1.71"] &&
	(path == "modules/ac/acimport_osuser.pike")) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      m_delete(rcs_file->tags, "ad_work_sync");
    }
    // FALL_THROUGH
  case "modules/ac/acintern_passwd.pike":
  case "modules/ac/userauth.pike":
    // These were moved in the AC/1.1 split.
    // CVSROOT/modules 1.50.
    // 98.11.10.11.34.00
    rename_revision(rcs_file, replace(path, "modules/ac/",
				      "modules/ac/modules/"), path,
		    append_revision(rcs_file, UNDEFINED, UNDEFINED,
				    "98.11.10.11.34.00",
				    "mast", "Börja på ny version av AC.\n"));
    break;

  case "modules/ac/.cvsignore":
    // This file was deleted in when the AC files were renamed.
    append_revision(rcs_file, "1.3", UNDEFINED, "98.10.20.03.15.52", "mast",
		    "Moved the libraries to the modules directory.\n", "dead");
    break;

  case "manager/hackers_overview.html":
    if (rcs_file->revisions["1.5"] &&
	(rcs_file->revisions["1.5"]->state == "dead")) {
      // Change the commit message to be compatible with
      // the initial commit of doc/hackers_overview.html.
      rcs_file->revisions["1.5"]->log = "File states take two.\n";
      // Make sure that the file shows up in the 1.3 branch as well.
      add_branch(rcs_file, "1.3", "1.5");
    }
    break;

    // SiteBuilder 2.0
  case "cvs-1.10-roxen.tar.gz":
    if (git->master_branch >= "2.0") {
      // This file was renamed in revision 1.7.
      rename_revision(rcs_file, "cvs-1.10-idonex.tar.gz", path, "1.7");
    }
    break;

  case "templates/.no_modules":
  case "templates/content_editor.tmpl":
    // The old templates were moved to themes/trad/ at this point.
    if (git->master_branch == "1.3") {
      string rev = append_revision(rcs_file, "2.0", UNDEFINED,
				   "2000.02.10.14.55.05", "jonasw",
				   "Theme support!\n", "dead");
    }
    break;

  case "default/img/background.gif":
  case "default/img/black_bg.gif":
  case "default/img/folder-open.gif":
  case "default/img/folder.gif":
  case "default/img/left_bg.gif":
  case "default/img/pike-logo.gif":
  case "default/img/sb.gif":
  case "default/img/text.gif":
  case "default/img/top.gif":
  case "default/img/white_bg.gif":
  case "default/index.html":
  case "default/search.html":
  case "default/templates/default.tmpl":
  case "default/top.menu":
    if (git->master_branch == "1.3") {
      // The default site was replaced with the DemoLabs site at some
      // point in time. We've selected the earliest point in time.
      // demolabs/index.xml:1.1 2000.04.07.07.11.14
      append_revision(rcs_file, "2.0", UNDEFINED,
		      "2000.04.07.07.11.14", "jonasw",
		      "First version of DemoLabs site.\n", "dead");
    }
    break;

  case "modules/navigation/navigation.pike":
    // Zino had a sticky revision on this file for quite a while.
    if (git->master_branch != "1.0") {
      sticky_revision(rcs_file, "99.01.14.04.32.48", "zino",
		      "1.65", "1.68", "1.68.2.1", ({ "roxen_1_2_61", }));
      sticky_revision(rcs_file, "99.02.02.07.53.02", "zino",
		      "1.65", "1.68", "1.68.4.1",
		      ({ "roxen_1_2_72", "roxen_1_2_73",
			 "roxen_1_2_74", "roxen_1_2_75" }));
    }
    // As did the forsmark releases by mast.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.75", "1.76", "1.76.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.75", "1.76", "1.76.4.1", ({ "forsmark_20000214", }));
      sticky_revision(rcs_file, "2000.02.20.01.10.01", "mast",
		      "1.75", "1.79", "1.79.6.1", ({ "forsmark_20000218", }));
    }
    break;

  case "modules/manager/sitebuilder.pike":
    // Sticky tags for forsmark.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.30", "1.33", "1.33.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.30", "1.33", "1.33.4.1", ({ "forsmark_20000214", }));
    }
    if ((git->master_branch >= "2.1") && (rcs_file->revisions["1.59.2.6"])) {
      // Add a merge link for the sitebuilder_2.1_replicate branch.
      rcs_file->revisions["1.61"]->merges = ({ "1.59.2.6" });
    }
    break;

  case "modules/manager/sitebuilder_tags.pike":
    // Sticky tags for forsmark.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.44", "1.46", "1.46.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.44", "1.46", "1.46.4.1", ({ "forsmark_20000214", }));
    }
    break;

  case "pike-modules/Sitebuilder.pmod/Site.pike":
    // Sticky tags for forsmark.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.79", "1.81", "1.81.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.79", "1.81", "1.81.4.1", ({ "forsmark_20000214", }));
      sticky_revision(rcs_file, "2000.02.20.01.10.01", "mast",
		      "1.79", "1.81", "1.81.6.1", ({ "forsmark_20000218", }));
    }
    if ((git->master_branch >= "2.1") && (rcs_file->revisions["1.174.2.5"])) {
      rcs_file->tags["sitebuilder_2_1_210_workflow"] =
	rcs_file->tags["sitebuilder_2_1_211_workflow"] = "1.174.2.5";
    }
    break;

  case "pike-modules/VC.pmod/CVS.pmod/cachedir.pike":
    // Sticky tags for forsmark.
    if (git->master_branch == "1.3") {
      sticky_revision(rcs_file, "2000.02.03.11.00.00", "mast",
		      "1.44", "1.45", "1.45.2.1", ({ "forsmark_20000203", }));
      sticky_revision(rcs_file, "2000.02.14.11.00.00", "mast",
		      "1.44", "1.45", "1.45.4.1", ({ "forsmark_20000214", }));
    }
    break;

  case "modules/manager/mirror_sitebuilder.pike":
    if (git->master_branch == "1.3") {
      // This module was developed in the 1.3 tree and
      // later copied to the 2.0 tree.
      kill_branch(rcs_file, "sitebuilder_2_0_freeler");
    }
    break;

  case "VERSION":
    if ((git->master_branch >= "2.1") && rcs_file->revisions["1.210.2.8"]) {
      // Add a merge link for the sitebuilder_2.1_workflow branch.
      string merge_rev =
	append_revision(rcs_file, UNDEFINED, "1.221", "2000.11.30.17.49.09",
			"mast", "Joined changes from the "
			"sitebuilder_2_1_workflow branch.\n", "Exp");
      rcs_file->revisions[merge_rev]->merges = ({ "1.210.2.8" });
    }
    if ((git->master_branch == "5.1") && rcs_file->revisions["1.1588.2.1"] &&
	rcs_file->revisions["1.1603"]) {
      // Add a reverse merge link for the forms2 branch.
      rcs_file->revisions["1.1588.2.1"]->merges = ({ "1.1603" });
    }
    // FALL_THROUGH
  case "pike-modules/Sitebuilder.pmod/MDFile.pike":
    // Make sure not to be tagged by this 1.3 tag.
    kill_tag(rcs_file, "released_to_holland_20000203");
    kill_tag(rcs_file, "forsmark_20000203");
    break;

  case "doc/.cvsignore":
  case "doc/Makefile":
  case "doc/VC.CVS.doc":
  case "doc/pmmltohtml.pike":
  case "pike-modules/.no_modules":
  case "TODO":
  case "how-to-install":
  case "manager/ftp_filesystem.pike":
  case "manager/idea":
  case "manager/sitebuilder_tags.pike":
  case "manager/img/.cvsignore":
  case "manager/img/undeleted.gif":
  case "manager/img/movedaway.gif":
  case "manager/img/selected_workarea.gif":
  case "manager/img/modified.gif":
  case "manager/img/.no_modules":
  case "manager/img/removed.gif":
  case "manager/img/make_images.sh":
  case "manager/img/undelmode.gif":
  case "manager/img/join.gif":
  case "manager/img/new.gif":
  case "manager/img/movedhere.gif":
  case "manager/img/exists.gif":
  case "manager/img/make_unselected_workarea.pike":
  case "manager/handlers/.no_modules":
  case "manager/handlers/default/handler.pike":
  case "manager/handlers/html/handler.pike":
  case "manager/handlers/image/handler.pike":
  case "manager/handlers/menu/handler.pike":
  case "manager/handlers/template/handler.pike":
  case "manager/handlers/text/handler.pike":
  case "manager/tabs/03:space":
  case "manager/tabs/15:templates/page.pike":
  case "manager/tabs/01:workareas/wizards/split.pike":
  case "manager/tabs/01:workareas/wizards/remove.pike":
  case "manager/tabs/01:workareas/wizards/config.pike":
  case "manager/tabs/01:workareas/page.pike":
  case "manager/tabs/10:files/wizards/update_dir.pike":
  case "manager/tabs/10:files/wizards/remove_ppoint.pike":
  case "manager/tabs/10:files/wizards/join.pike":
  case "manager/tabs/10:files/wizards/edit.pike":
  case "manager/tabs/10:files/wizards/commit_dir.pike":
  case "manager/tabs/10:files/wizards/move_directory.pike":
  case "manager/tabs/10:files/wizards/update_file.pike":
  case "manager/tabs/10:files/wizards/editmd.h":
  case "manager/tabs/10:files/wizards/remove.pike":
  case "manager/tabs/10:files/wizards/create_ppoint.pike":
  case "manager/tabs/10:files/wizards/upload_new.pike":
  case "manager/tabs/10:files/wizards/edit_metadata.pike":
  case "manager/tabs/10:files/wizards/undelete.pike":
  case "manager/tabs/10:files/wizards/commit_file.pike":
  case "manager/tabs/10:files/wizards/log.pike":
  case "manager/tabs/10:files/wizards/move.pike":
  case "manager/tabs/10:files/wizards/newdir.pike":
  case "manager/tabs/10:files/wizards/import_from_directory.pike":
  case "manager/tabs/10:files/wizards/annotate.pike":
  case "manager/tabs/10:files/wizards/remove_local.pike":
  case "manager/tabs/10:files/wizards/newfiles.pike":
  case "manager/tabs/10:files/wizards/undelete_directory.pike":
  case "manager/tabs/10:files/wizards/create_file.pike":
  case "manager/tabs/10:files/wizards/revert.pike":
  case "manager/tabs/10:files/wizards/upload.pike":
  case "manager/tabs/10:files/wizards/remove_directory.pike":
  case "manager/tabs/10:files/wizards/update.h":
  case "manager/tabs/10:files/wizards/commit.h":
  case "manager/tabs/10:files/page.pike":
  case "manager/tabs/20:preferences/wizards/user_preferences.pike":
  case "manager/tabs/20:preferences/wizards/commit_messages.pike":
  case "manager/tabs/20:preferences/wizards/content_types.pike":
  case "manager/tabs/20:preferences/wizards/languages.pike":
  case "manager/tabs/20:preferences/page.pike":
  case "manager/tabs/02:security/wizards/force_login.pike":
  case "manager/tabs/02:security/page.pike":
  case "manager/tabs/.no_modules":
  case "manager/tabs/30:plugins/page.pike":
    // The above files were not copied to newsitebuilder at the split.
    // 98.10.17.08.53.55
    append_revision(rcs_file, "1.3", UNDEFINED, "98.10.17.07.53.55", "mast",
		    "Imported from the old repository.\n", "dead");
    break;

    // SiteBuilder/2.1
  case "sites/empty/site/.do-not-remove-this-directory":
    // Make a soft reference for the branch commit, so that the number of
    // parallel branches can be reduced.
    rcs_file->revisions["1.1.2.1"]->revision_flags |= GIT_REVISION_MERGE;
    break;

  case "pike-modules/Sitebuilder.pmod/VCFile.pike":
    if ((git->master_branch >= "2.1") && rcs_file->revisions["1.178.2.6"]){
      rcs_file->tags["sitebuilder_2_1_210_workflow"] =
	rcs_file->tags["sitebuilder_2_1_211_workflow"] = "1.178.2.8";
      rcs_file->tags["sitebuilder_2_1_209_workflow"] =
	rcs_file->tags["sitebuilder_2_1_208_workflow"] =
	rcs_file->tags["sitebuilder_2_1_207_workflow"] = "1.178.2.6";
    }
    break;

  case "tabs/files/index.pike":
    if ((git->master_branch >= "2.1") && (rcs_file->revisions["1.174.2.4"])) {
      rcs_file->tags["sitebuilder_2_1_210_workflow"] =
	rcs_file->tags["sitebuilder_2_1_211_workflow"] = "1.174.2.4";
    }
    break;

  case "pike-modules/Replicate.pmod/thread.h":
    if ((git->master_branch >= "2.1") && rcs_file->revisions["1.1.2.1"]) {
      // Noring committed this file on the master branch rather
      // than on the sitebuilder_2.1_replicate branch.
      rcs_file->revisions["1.1.2.1"]->revision_flags |= GIT_REVISION_MERGE;
    }
    // FALL_THROUGH
  case "img/added.png":
  case "pike-modules/Replicate.pmod/Protocol.pmod":
  case "pike-modules/Replicate.pmod/Schedule.pike":
  case "pike-modules/Replicate.pmod/Script.pike":
    if (git->master_branch >= "2.1") {
      kill_branch(rcs_file, "sitebuilder_2_0_freeler");
    }
    break;

    // SiteBuilder/2.2
  case "pike-modules/Sitebuilder.pmod/FS.pmod/VCFile.pike":
  case "pike-modules/Sitebuilder.pmod/FS.pmod/VCDir.pike":
    if (git->master_branch >= "2.2") {
      // These were moved when FS.pmod was created.
      // 2001.03.29.15.40.24
      rename_revision(rcs_file,
		      replace(path, "pike-modules/Sitebuilder.pmod/FS.pmod/",
			      "pike-modules/Sitebuilder.pmod/"), path,
		      append_revision(rcs_file, UNDEFINED, UNDEFINED,
				      "2001.03.29.15.40.24",
				      "leif", "New SBObject/SBFile/SBDir abstraction layer.\n"));
    }
    break;

  case "pike-modules/Sitebuilder.pmod/FS.pmod/SBFileData.pike":
  case "pike-modules/Sitebuilder.pmod/FS.pmod/SBFileConflict.pike":
    if (git->master_branch >= "2.2") {
      // These were renamed when FS.pmod was created.
      // 2001.03.29.15.40.24
      rename_revision(rcs_file,
		      replace(path, "pike-modules/Sitebuilder.pmod/FS.pmod/SBFile",
			      "pike-modules/Sitebuilder.pmod/VCFile"), path,
		      append_revision(rcs_file, UNDEFINED, UNDEFINED,
				      "2001.03.29.15.40.24",
				      "leif", "New SBObject/SBFile/SBDir abstraction layer.\n"));
    }
    break;

  case "pike-modules/Replicate.pmod/Frontend.pmod/Script.pike":
    if (git->master_branch >= "2.2") {
      rename_revision(rcs_file,
		      "pike-modules/Replicate.pmod/Script.pike", path,
		      append_revision(rcs_file, UNDEFINED, UNDEFINED,
				      "2001.05.08.12.00.01",
				      "noring", "Replication backend 2.2.\n"));
    }
    break;

  case "tabs/prefs/wizards/rep_settings.pike":
    if (git->master_branch >= "2.2") {
      rename_revision(rcs_file, "tabs/prefs/wizards/replicate.pike", path,
		      "1.10");
    }
    break;

    // SiteBuilder/3.3
  case "modules/manager/insite_editor.pike":
    if (git->master_branch >= "5.2") {
      // The forms2 branch was merged into the 5.2 branch
      // after the 5.1/5.2 split.
      if (rcs_file->revisions["1.322.2.1"] &&
	  rcs_file->revisions["1.325"]) {
	rcs_file->revisions["1.325"]->merges = ({ "1.322.2.1" });
      }
    }
    // FALL_THROUGH
  case "insite-files/cms-templates/cms-components.xsl":
  case "insite-files/cms-templates/diagram-comp.xsl":
  case "insite-files/cms-templates/href-comp.xsl":
  case "insite-files/cms-templates/navigation.xsl":
  case "insite-files/cms-templates/news-comp.xsl":
  case "insite-files/cms-templates/page-layout.xsl":
  case "insite-files/cms-templates/picture-comp.xsl":
  case "insite-files/cms-templates/rxml-comp.xsl":
  case "insite-files/cms-templates/table-comp.xsl":
  case "modules/editor/diagram-component.pike":
  case "modules/editor/footer-component.pike":
  case "modules/editor/header-component.pike":
  case "modules/editor/href-component.pike":
  case "modules/editor/news-component.pike":
  case "modules/editor/picture-component.pike":
  case "pike-modules/Sitebuilder.pmod/Editor.pmod":
  case "themes/dflt/img/cms-small.psd":
  case "themes/dflt/img/editor-delete-icon.psd":
  case "themes/dflt/img/editor-edit-icon.psd":
  case "themes/dflt/img/editor-new-icon.psd":
    // These files were added after the 2.4/3.3 split, and should
    // not be merged into the rtl_garbage_free_xslt branch.
    kill_branch(rcs_file, "rtl_garbage_free_xslt");
    break;

    // SiteBuilder/3.4
  case "modules/ac/pike-modules/NewLDAP.pmod/ldap_errors.h":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.1.2.4"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was deleted, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.1.2.4";
    }
    break;
  case "modules/ac/acimport_ldap.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.44.2.13"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.44.2.13";
    }
    break;
  case "modules/ac/acimport_ad.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.5.2.13"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.5.2.13";
    }
    break;
  case "modules/ac/acbackend_mysql.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.12.2.2"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.12.2.2";
    }
    if (git->master_branch >= "5.0") {
      // It seems this tag mistakenly was put on a revision
      // on the master branch for this file.
      m_delete(rcs_file->tags, "sitebuilder_5_0_workflow_merge1");
    }
    break;
  case "modules/ac/actags.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.10.2.2"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.10.2.2";
    }
    break;
  case "modules/ac/acimport_sql.pike":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.4.2.1"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.4.2.1";
    }
    break;
  case "modules/ac/pike-modules/AC.pmod":
    if ((git->master_branch == "3.4") && rcs_file->revisions["1.56.2.6"]) {
      // It seems that the ad_work_sync tag was moved each time
      // the branches were synced. In the last sync, this file
      // was missed, and thus kept its old revision.
      rcs_file->tags["ad_work_sync"] = "1.56.2.6";
    }
    break;

    // SiteBuilder/4.0
  case "tabs/plugins/index.html":
  case "tabs/plugins/index.pike":
    if (git->master_branch == "3.4") {
      // These files were deleted sometime after the SiteBuilder 4.0 split.
      // 2004.02.03.13.37.32
      append_revision(rcs_file, "4.0", UNDEFINED, "2004.02.03.13.37.32",
		      "anders",
		      "Removed references to deleted files.\n", "dead");
    }
    break;

  case "themes/trad/big_button.xcf":
  case "themes/trad/gbutton.xcf":
  case "themes/trad/img/buttons/Access.jpg":
  case "themes/trad/img/buttons/Admin.jpg":
  case "themes/trad/img/buttons/Edit.jpg":
  case "themes/trad/img/buttons/File.jpg":
  case "themes/trad/img/buttons/Versions.jpg":
  case "themes/trad/img/buttons/View.jpg":
  case "themes/trad/img/delete.gif":
  case "themes/trad/img/divider.gif":
  case "themes/trad/img/down-arrow.gif":
  case "themes/trad/img/down-arrow.png":
  case "themes/trad/img/fold.gif":
  case "themes/trad/img/fold_all.gif":
  case "themes/trad/img/fold_all.png":
  case "themes/trad/img/go_up.gif":
  case "themes/trad/img/go_up.png":
  case "themes/trad/img/menu-back.gif":
  case "themes/trad/img/menu-back.png":
  case "themes/trad/img/menu-open.gif":
  case "themes/trad/img/menu-open.png":
  case "themes/trad/img/menu.gif":
  case "themes/trad/img/menu.png":
  case "themes/trad/img/menusep.gif":
  case "themes/trad/img/movedown.gif":
  case "themes/trad/img/moveup.gif":
  case "themes/trad/img/moveupordown.gif":
  case "themes/trad/img/next-double.gif":
  case "themes/trad/img/next-double.png":
  case "themes/trad/img/next.gif":
  case "themes/trad/img/next.png":
  case "themes/trad/img/popup-badge.gif":
  case "themes/trad/img/popup-badge.png":
  case "themes/trad/img/prev-double.gif":
  case "themes/trad/img/prev-double.png":
  case "themes/trad/img/prev.gif":
  case "themes/trad/img/prev.png":
  case "themes/trad/img/rightarrow.gif":
  case "themes/trad/img/roxen-cms.gif":
  case "themes/trad/img/roxen-sb.gif":
  case "themes/trad/img/toggle.gif":
  case "themes/trad/img/unfold.gif":
  case "themes/trad/img/unfold_all.gif":
  case "themes/trad/img/unfold_all.png":
  case "themes/trad/tabframe.xcf":
  case "themes/trad/templates/content_editor.tmpl":
  case "themes/trad/theme":
  case "themes/trad/theme.pike":
    if (git->master_branch == "3.4") {
      // These files were deleted sometime after the SiteBuilder 4.0 split.
      // And probably even after the 4.5 split.
      // 2004.09.17.15.37.10
      append_revision(rcs_file, "4.0", UNDEFINED, "2004.09.17.15.37.10",
		      "jonasw",
		      "Don't honor old theme settings.\n", "dead");
      string rev =
	append_revision(rcs_file, "4.5", UNDEFINED, "2004.09.17.15.37.10",
			"jonasw",
			"Don't honor old theme settings.\n", "dead");
      add_branch(rcs_file, "5.0", rev);
      add_branch(rcs_file, "5.1", rev);
      add_branch(rcs_file, "5.2", rev);
    }
    break;


  case "modules/ac/pike-modules/NewLDAP.pmod/client.pike":
  case "modules/ac/pike-modules/NewLDAP.pmod/ldap_globals.h":
  case "modules/ac/pike-modules/NewLDAP.pmod/ldap_privates.pmod":
  case "modules/ac/pike-modules/NewLDAP.pmod/module.pmod":
  case "modules/ac/pike-modules/NewLDAP.pmod/protocol.pike":
    if (git->master_branch >= "4.0") {
      // These were initially added on the main branch.
      if (rcs_file->revisions["1.2.2.1"]) {
	// rcs_file->revisions["1.2.2.1"]->revision_flags |= GIT_REVISION_MERGE;
	// Disconnect the branch.
	rcs_file->revisions["1.2.2.1"]->ancestor = UNDEFINED;
      }
    }
    break;

  case "insite-files/print/".."insite-files/print/\377":
    // These files were added on the 4.0 branch after the 4.0/4.5 split.
    kill_branch(rcs_file, "4.5");
    kill_branch(rcs_file, "5.0");
    kill_branch(rcs_file, "5.1");
    kill_branch(rcs_file, "5.2");
    break;

    // SiteBuilder/4.5
  case "sites/basic-4.5/site/news/2006/".."sites/basic-4.5/site/news/2006/\377":
    {
      // The news items were moved during 4.5.
      string rev;
      if (has_suffix(path, "/index.xml")) {
	rev = find_revision(rcs_file, UNDEFINED, "2006.02.07.12.01.56");
      } else {
	rev = append_revision(rcs_file, UNDEFINED, UNDEFINED,
			      "2006.02.07.12.01.54", "jonasw",
			      "2005 -> 2006.\n");
      }
      rename_revision(rcs_file, replace(path, "/2006/", "/2005/"), path, rev);
    }
    break;

  case "insite-files/cms-sites/4.5/newsletter/stat":
  case "modules/misc/newsletter.pike":
  case "themes/insite/newsletter.css":
    if (git->master_branch == "4.5") {
      // These files were copied from SiteBuilder/5.0.
      // Make sure they aren't merged into SiteBuilder/5.0.
      kill_branch(rcs_file, "5.0");
      kill_branch(rcs_file, "sitebuilder_5_0_workflow");
      kill_branch(rcs_file, "forms2");
    }
    break;

  case "modules/editor/fck_editor2/editor/dialog/fck_roxenspellcheck.html":
    if (git->master_branch >= "4.5") {
      // For some reason this dead revision doesn't
      // get merged into the main 4.5 branch properly.
      rcs_file->tags["sitebuilder_4_5_1"] = "1.4";
      rcs_file->tags["sitebuilder_4_5_72"] = "1.4";
    }
    break;

  case "modules/editor/fck_editor2/fckconfig.js":
    // These files erroneously got the tag FCK_2_4_3 after
    // the FCK_2_5_1 import in SiteBuilder 4.5.
    if ((git->master_branch == "4.5") && rcs_file->tags["FCK_2_4_3"] &&
	(rcs_file->tags["FCK_2_4_3"] == rcs_file->tags["FCK_2_5_1"]))	{
      string rev = m_delete(rcs_file->tags, "FCK_2_4_3");
      if (!has_suffix(rev, ".1") && (rev[-2] == '.')) {
	// Move the tag to the previous revision.
	rev = rev[..sizeof(rev)-2] + (((int)rev[sizeof(rev)-1..])-1);
	rcs_file->tags["FCK_2_4_3"] = rev;
      }
    }
    // FALL_THROUGH
  case "modules/editor/fck_editor2/editor/_source/commandclasses/fck_othercommands.js":
  case "modules/editor/fck_editor2/editor/js/fckeditorcode_gecko_2.js":
  case "modules/editor/fck_editor2/editor/js/fckeditorcode_ie_2.js":
    // These files had a vendor branch that acted as the main branch
    // for a while, even though they earlier have manual patches.
    // Make sure this is handled correctly by adding appropriate
    // "merge" commits on the main branch.
    if (git->master_branch >= "4.5") {
      string ancestor_rev = ([
	"fckeditorcode_gecko_2.js":"1.6",
	"fckconfig.js":"1.6",
	"fckeditorcode_ie_2.js":"1.4",
	"fck_othercommands.js":"1.3",
      ])[basename(path)];
      foreach(({ "1.1.1.3", "1.1.1.4", "1.1.1.5", "1.1.1.6",
		 "1.1.1.7", "1.1.1.8", "1.1.1.9", }),
	      string vendor_rev) {
	RCSFile.Revision rev = rcs_file->revisions[vendor_rev];
	if (!rev) continue;

	// Commit messages for the merges.
	string message = ([
	  "1.1.1.3":"mast testar.\n",
	  "1.1.1.4":"FCKEditor config file\n",
	  "1.1.1.5":"Forgot to remove unused code.\n",
	  "1.1.1.6":"join latest fckrelease 3.2.1\n",
	  "1.1.1.7":"Reinstated Roxen CMS patch for disabling Table Properties context menu command.\n",
	  "1.1.1.8":"Reinstated Roxen CMS patch for disabling Table Properties context menu command.\n",
	  "1.1.1.9":"Enable FCKEditor for sufficent new AppleWebKit (Safari3+)\n",
	])[vendor_rev];
	int delta = ([
	  "1.1.1.3":1400,
	  "1.1.1.4":3600,
	  "1.1.1.5":600,
	  "1.1.1.6":600,
	  "1.1.1.7":360,
	  "1.1.1.8":520,
	  "1.1.1.9":2400,
	])[vendor_rev];
	rev = rcs_file->append_revision(vendor_rev, ancestor_rev,
					rev->time + delta, rev->author,
					message);
	if (ancestor_rev == rcs_file->head) {
	  rcs_file->head = rev->revision;
	}
	ancestor_rev = rev->revision;
	// Move any branches from the vendor branch to the merge.
	if (rev->branches = rcs_file->revisions[vendor_rev]->branches) {
	  rcs_file->revisions[vendor_rev]->branches = 0;
	  foreach(rev->branches, string br) {
	    RCSFile.Revision brev = rcs_file->revisions[br];
	    if (brev->ancestor == vendor_rev) {
	      brev->ancestor = ancestor_rev;
	    }
	  }
	}
	// hide_revision(rcs_file, ancestor_rev);
	// Move the appropriate tags to the master branch.
	foreach(rcs_file->tags; string tag; string r) {
	  if (has_prefix(tag, "FCK")) continue;
	  if (r == vendor_rev) {
	    rcs_file->tags[tag] = ancestor_rev;
	  } else if (has_prefix(r, vendor_rev + ".0.")) {
	    r = ((r/".") - ({ "0" })) * ".";
	    if (rcs_file->branch_heads[r] == vendor_rev) {
	      rcs_file->branch_heads[r] = ancestor_rev;
	    }
	  }
	}
      }
    }
    break;

  case "modules/editor/fck_editor2/_whatsnew.html":
    // These files erroneously got the tag FCK_2_4_3 after
    // the FCK_2_5_1 import in SiteBuilder 4.5.
    if ((git->master_branch == "4.5") && rcs_file->tags["FCK_2_4_3"] &&
	(rcs_file->tags["FCK_2_4_3"] == rcs_file->tags["FCK_2_5_1"]))	{
      string rev = m_delete(rcs_file->tags, "FCK_2_4_3");
      if (!has_suffix(rev, ".1") && (rev[-2] == '.')) {
	// Move the tag to the previous revision.
	rev = rev[..sizeof(rev)-2] + (((int)rev[sizeof(rev)-1..])-1);
	rcs_file->tags["FCK_2_4_3"] = rev;
      }
    }
    // These files had a vendor branch that acted as the main branch
    // for a while, even though they earlier have manual patches.
    // Make sure this is handled correctly by adding appropriate
    // "merge" commits on the main branch.
    if (git->master_branch >= "4.5") {
      string ancestor_rev = ([
	"_whatsnew.html":"1.2",
      ])[basename(path)];
      foreach(({ "1.1.1.2", "1.1.1.3", "1.1.1.4", "1.1.1.5", "1.1.1.6",
		 "1.1.1.7", }),
	      string vendor_rev) {
	RCSFile.Revision rev = rcs_file->revisions[vendor_rev];
	if (!rev) continue;

	// Commit messages for the merges.
	string message = ([
	  "1.1.1.2":"FCKEditor config file\n",
	  "1.1.1.3":"Forgot to remove unused code.\n",
	  "1.1.1.4":"join latest fckrelease 3.2.1\n",
	  "1.1.1.5":"Reinstated Roxen CMS patch for disabling Table Properties context menu command.\n",
	  "1.1.1.6":"Reinstated Roxen CMS patch for disabling Table Properties context menu command.\n",
	  "1.1.1.7":"Enable FCKEditor for sufficent new AppleWebKit (Safari3+)\n",
	])[vendor_rev];
	int delta = ([
	  "1.1.1.2":3600,
	  "1.1.1.3":600,
	  "1.1.1.4":600,
	  "1.1.1.5":360,
	  "1.1.1.6":520,
	  "1.1.1.7":2400,
	])[vendor_rev];
	rev = rcs_file->append_revision(vendor_rev, ancestor_rev,
					rev->time + delta, rev->author,
					message);
	if (ancestor_rev == rcs_file->head) {
	  rcs_file->head = rev->revision;
	}
	ancestor_rev = rev->revision;
	// Move any branches from the vendor branch to the merge.
	if (rev->branches = rcs_file->revisions[vendor_rev]->branches) {
	  rcs_file->revisions[vendor_rev]->branches = 0;
	  foreach(rev->branches, string br) {
	    RCSFile.Revision brev = rcs_file->revisions[br];
	    if (brev->ancestor == vendor_rev) {
	      brev->ancestor = ancestor_rev;
	    }
	  }
	}
	// hide_revision(rcs_file, ancestor_rev);
	// Move the appropriate tags to the master branch.
	foreach(rcs_file->tags; string tag; string r) {
	  if (has_prefix(tag, "FCK")) continue;
	  if (r == vendor_rev) {
	    rcs_file->tags[tag] = ancestor_rev;
	  } else if (has_prefix(r, vendor_rev + ".0.")) {
	    r = ((r/".") - ({ "0" })) * ".";
	    if (rcs_file->branch_heads[r] == vendor_rev) {
	      // Branch without commits. Move it to the merge.
	      m_delete(rcs_file->tags, tag);
	      add_branch(rcs_file, tag, ancestor_rev);
	    }
	  }
	}
      }
    }
    break;

  case "modules/editor/fck_editor2/_whatsnew_history.html":
  case "modules/editor/fck_editor2/editor/_source/internals/fcklistslib.js":
  case "modules/editor/fck_editor2/fckeditor.afp":
  case "modules/editor/fck_editor2/fckeditor.asp":
  case "modules/editor/fck_editor2/fckeditor.cfc":
  case "modules/editor/fck_editor2/fckeditor.cfm":
  case "modules/editor/fck_editor2/fckeditor.js":
  case "modules/editor/fck_editor2/fckeditor.lasso":
  case "modules/editor/fck_editor2/fckeditor.php":
  case "modules/editor/fck_editor2/fckeditor.pl":
  case "modules/editor/fck_editor2/fckeditor.py":
  case "modules/editor/fck_editor2/fckpackager.xml":
    // These files erroneously got the tag FCK_2_4_3 after
    // the FCK_2_5_1 import in SiteBuilder 4.5.
    if ((git->master_branch == "4.5") && rcs_file->tags["FCK_2_4_3"] &&
	(rcs_file->tags["FCK_2_4_3"] == rcs_file->tags["FCK_2_5_1"]))	{
      string rev = m_delete(rcs_file->tags, "FCK_2_4_3");
      if (!has_suffix(rev, ".1") && (rev[-2] == '.')) {
	// Move the tag to the previous revision.
	rev = rev[..sizeof(rev)-2] + (((int)rev[sizeof(rev)-1..])-1);
	rcs_file->tags["FCK_2_4_3"] = rev;
      }
    }
    break;

    // SiteBuilder/5.0
  case "sites/basic/".."sites/basic/\377":
    if (has_prefix(path, "sites/basic/site/news/2006/")) {
      // The news items were moved during 4.5.
      string rev;
      if (has_suffix(path, "/index.xml")) {
	rev = find_revision(rcs_file, UNDEFINED, "2006.02.07.12.08.55");
      } else {
	rev = append_revision(rcs_file, UNDEFINED, UNDEFINED,
			      "2006.02.07.12.08.54", "jonasw",
			      "2004 -> 2006.\n");
      }
      rename_revision(rcs_file, replace(path, "/2006/", "/2004/"), path, rev);
    }
    // FALL_THROUGH
  case "sites/demolabs/".."sites/demolabs/\377":
  case "insite-files/i-".."insite-files/i-\377":
    // These files were deleted in the SiteBuilder 5.0 split.
    // 2006.10.31.10.32.26 (the actual split was actually the day before).
    if (git->master_branch == "4.5") {
      string rev =
	append_revision(rcs_file, "5.0", UNDEFINED, "2006.10.31.10.32.26",
			"anders",
			"Added optional cropping function to picture type component fields.\n", "dead");
      add_branch(rcs_file, "5.1", rev);
      add_branch(rcs_file, "5.2", rev);
    }
    break;

  case "modules/editor/fck_editor/".."modules/editor/fck_editor/\377":
    if (git->master_branch == "4.5") {
      // These were deleted when the support for FCK 1.0 was removed in 5.0.
      string rev =
	append_revision(rcs_file, "5.0", UNDEFINED, "2006.11.02.16.20.46",
			"jonasw", "Remove code related to FCK Editor 1.x.\n",
			"dead");
      add_branch(rcs_file, "5.1", rev);
      add_branch(rcs_file, "5.2", rev);
      // And later the files were moved to the attic in 4.5 as well,
      // but not killed outright.
      // The directory was last modified 2008-01-11T20:49:29,
      // and the last tag the files have is sitebuilder_4_5_208
      // from earlier that day.
      append_revision(rcs_file, UNDEFINED, UNDEFINED, "2008.01.11.21.37.24",
		      "jonasw", "Deprecated CMS_USE_FCK1.\n", "dead");
    }
    break;

  case "pike-modules/Sitebuilder.pmod/Workflow.pike":
    if ((git->master_branch >= "5.0") && rcs_file->revisions["1.1.2.16"]) {
      // Add a merge link for the sitebuilder_5.0_workflow branch.
      rcs_file->revisions["1.2"]->merges = ({ "1.1.2.16" });
    }
    break;

  case "insite-files/cms-sites/4.5/forms/strings.xsl":
  case "insite-files/cms-sites/4.5/strings.xsl":
  case "insite-files/cms-templates/forms/strings.xsl":
  case "insite-files/cms-templates/strings.xsl":
    if (git->master_branch >= "5.0") {
      // Some time after the 4.5/5.0 split these files were changed
      // to be binary.
      // This seems to have been done in conjunction with
      // insite-files/cms-templates/strings.xsl:1.14
      // 2009.01.09.15.36.21
      RCSFile.Revision r;
      if (path == "insite-files/cms-templates/strings.xsl") {
	r = rcs_file->revisions["1.14"];
      } else {
	r = rcs_file->
	  revisions[append_revision(rcs_file, UNDEFINED, UNDEFINED,
				    "2009.01.09.15.36.21", "mast",
				    "Too dangerous to have cvs expansions in "
				    "multilang files. (Marked those files\n"
				    "with -kb now, too.)\n")];
      }
      // Adjust all the older revisions to be non-binary,
      // so that they are compatible with the older branches.
      foreach(rcs_file->revisions;; RCSFile.Revision rev) {
	if (rev->time >= r->time) continue;	// After the change.
	if (rev->revision_flags & GIT_EXPAND_GUESS) {
	  rev->revision_flags = GIT_EXPAND_ALL;	// Force non-binary.
	}
      }
    }
    break;
  case "modules/ac/acauth_gssapi.pike":
    if (rcs_file->revisions["1.2.2.1"]) {
      // Make a soft reference for the branch commit, so that the number of
      // parallel branches can be reduced.
      rcs_file->revisions["1.2.2.1"]->revision_flags |= GIT_REVISION_MERGE;
    }
    break;
  case "pike-modules/Sitebuilder.pmod/ReIndexer.pike":
    if (rcs_file->revisions["1.3.2.1"]) {
      // Make a soft reference for the branch commit, so that the number of
      // parallel branches can be reduced.
      rcs_file->revisions["1.3.2.1"]->revision_flags |= GIT_REVISION_MERGE;
    }
    break;

    // SiteBuilder/5.1

    // SiteBuilder/5.2
  }
  if (git->master_branch == "1.0") {
    // Zap known problematic tags.
    foreach(rcs_file->tags; string tag;) {
      if (has_prefix(tag, "roxen_") || has_prefix(tag, "forsmark_") ||
	  has_prefix(tag, "sitebuilder_") || has_prefix(tag, "Forsmark_") ||
	  has_prefix(tag, "release") || (tag == "platform_best_try")) {
	m_delete(rcs_file->tags, tag);
      }
    }
  } else if (git->master_branch == "1.3") {
    // Zap known problematic tags.
    foreach(rcs_file->tags; string tag;) {
      if (has_prefix(tag, "sitebuilder_2_") ||
	  has_prefix(tag, "sitebuilder_1_4_")) {
	m_delete(rcs_file->tags, tag);
      }
    }
  } else {
    foreach(({ "released_to_holland_20000203", "forsmark_20000214",
	       "forsmark_20000218" }), string tag) {
      if (rcs_file->tags[tag] != "0.0") {
	m_delete(rcs_file->tags, tag);
      }
    }
  }
  if (git->master_branch >= "2.1") {
    // sitebuilder_2_0_32 .. sitebuilder_2_0_46
    // The VERSION file was bumped to 2.1 late...
    // Rename the broken tags accordingly.
    if (has_prefix(path, "modules/xsltransform/")) {
      // XSLT/2.1 has some actual 2.0 tags in the range.
      // Remove them.
      m_delete(rcs_file->tags, "sitebuilder_2_0_36");
      m_delete(rcs_file->tags, "sitebuilder_2_0_37");
      m_delete(rcs_file->tags, "sitebuilder_2_0_38");
    }
    foreach(rcs_file->tags; string tag; string rev) {
      if (has_prefix(tag, "sitebuilder_2_0_") &&
	  (tag >= "sitebuilder_2_0_32") &&
	  !has_suffix(tag, "_freeler")) {
	rcs_file->tags["sitebuilder_2_1_" +
		       tag[sizeof("sitebuilder_2_0_")..]] =
	  m_delete(rcs_file->tags, tag);
      }
    }
    if (has_prefix(path, "modules/javascript_support/")) {
      // This module was not included in SiteBuilder 2.2 and later.
      append_revision(rcs_file, "2.2", UNDEFINED,
		      "2000.11.30.20.03.18", "mast",
		      "Handle string group names in DATA_GID.\n", "dead");
    }

    foreach(rcs_file->tags; string tag; string rev) {
      // Tags 2.1.207 .. 2.1.214 were on both the main branch
      // and the sitebuilder_2.1_workflow branch.
      // Rename and reassign the tags accordingly.
      if ((tag >= "sitebuilder_2_1_207") && (tag <= "sitebuilder_2_1_214") &&
	  !has_suffix(tag, "_workflow")) {
	if (rcs_file->tags[tag + "_workflow"]) continue;
	// Note that there are some files that have tags from the branch,
	// and some from the master branch. We thus need to list
	// the exceptions below.
	if ((sizeof(rev/".") > 2) ||
	    (tag >= "sitebuilder_2_1_212") ||
	    (< "modules/manager/workarea.pike",
	       "pike-modules/Sitebuilder.pmod/VCFile.pike",
	       "pike-modules/Sitebuilder.pmod/VCFileConflict.pike",
	       "pike-modules/Sitebuilder.pmod/FS.pmod/SBFileConflict.pike",
	    >)[path]) {
	  rcs_file->tags[tag + "_workflow"] = m_delete(rcs_file->tags, tag);
	}
      }
    }
    if (path == "VERSION") {
      // Make sure the VERSION file gets the correct tags...
      foreach(({ "207", "208", "209", "210", "211", "212", "213", "214" });
	      int i; string build) {
	string tag = "sitebuilder_2_1_" + build;
	if (rcs_file->tags[tag]) {
	  tag += "_workflow";
	  rcs_file->tags[tag] = "1.210.2." + (i + 1);
	} else {
	  rcs_file->tags[tag] = "1." + (i + 211);
	}
      }
    }
  } else if (path == "modules/weblayout/weblayout.pike") {
    // The weblayout module was shared between 2.0 and 2.1.
    foreach(rcs_file->tags; string tag; string rev) {
      if (has_prefix(tag, "platform_2_1_") ||
	  has_prefix(tag, "sitebuilder_2_1_")) {
	m_delete(rcs_file->tags, tag);
      }
    }
  }
#if 0
  if (git->master_branch >= "2.2") {
    // sitebuilder_2_1_ .. sitebuilder_2_1_217 ==> .. sitebuilder_2_2_100
    // The VERSION file was bumped to 2.4 late...
    // Rename the broken tags accordingly.
    foreach(rcs_file->tags; string tag; string rev) {
      if (has_prefix(tag, "sitebuilder_3_3_") &&
	  (tag >= "sitebuilder_2_3_320")) {
	rcs_file->tags["sitebuilder_2_4_" +
		       tag[sizeof("sitebuilder_2_4_")..]] =
	  m_delete(rcs_file->tags, tag);
      }
    }
  }
#endif
  if (git->master_branch >= "2.4") {
    // sitebuilder_2_3_320 .. sitebuilder_2_3_433
    // The VERSION file was bumped to 2.4 late...
    // Rename the broken tags accordingly.
    foreach(rcs_file->tags; string tag; string rev) {
      if (has_prefix(tag, "sitebuilder_2_3_") &&
	  (tag >= "sitebuilder_2_3_320")) {
	rcs_file->tags["sitebuilder_2_4_" +
		       tag[sizeof("sitebuilder_2_3_")..]] =
	  m_delete(rcs_file->tags, tag);
      }
    }
  }
  if (git->master_branch >= "3.3") {
      // The VERSION file was bumped to 3.3 somewhat timely,
      // but the tagging script continued with 2.4...
      // Rename the broken tag accordingly.
    foreach(rcs_file->tags; string tag; string rev) {
      if (has_prefix(tag, "sitebuilder_2_4_") &&
	  (tag >= "sitebuilder_2_4_455")) {
	rcs_file->tags["sitebuilder_3_3_" +
		       tag[sizeof("sitebuilder_2_4_")..]] =
	  m_delete(rcs_file->tags, tag);
      }
    }
  }
  if (git->master_branch == "3.4") {
    // Give this partial branch some help.
    split_branch(rcs_file, "randstad_ad_work_2005_03", "2005.08.08.15.46.39");
  } else {
    if (rcs_file->revisions["1.1"]->time->unix_time() > 1054041819) {
      // File added after the 3.4/4.0 split.
      kill_branch(rcs_file, "randstad_ad_work_2005_03");
    }
  }
  if (rcs_file->tags["ad_work_sync"]) {
    // This tag exists in both 3.4 and 4.0, but for different revisions.
    if (git->master_branch == "3.4") {
      rcs_file->tags["ad_work_sync/3.4"] =
	m_delete(rcs_file->tags, "ad_work_sync");
    } else {
      rcs_file->tags["ad_work_sync/4.0"] =
	m_delete(rcs_file->tags, "ad_work_sync");
    }
  }
  if (git->master_branch == "4.0") {
    // Give this partial branch some help.
    split_branch(rcs_file, "randstad_ad_work_2005_11", "2005.11.14.12.58.48");
  } else {
    if (rcs_file->revisions["1.1"]->time->unix_time() > 1054041819) {
      // File added after the 3.4/4.0 split.
      kill_branch(rcs_file, "randstad_ad_work_2005_11");
    }
  }

  if (git->master_branch >= "4.5") {
    // The FCK_DIST branch tag is only on the fck_editor2 files.
    // Let's help it along.
    // The import of FCK_2.0_B2.
    split_branch(rcs_file, "FCK_DIST", "2004.10.01.14.36.22");
  }

  if (git->master_branch >= "5.0") {
    // Help some branches that are on very few files.
    split_branch(rcs_file, "sitebuilder_5_0_workflow", "2008.04.02.16.04.35");

    // These four tags were mislabled due to a missing bump of VERSION.
    foreach(({ "sitebuilder_4_5_116", "sitebuilder_4_5_117",
	       "sitebuilder_4_5_118", "sitebuilder_4_5_119",
	    }), string tag) {
      if (rcs_file->tags[tag]) {
	// Rename the tag to its proper name.
	string new_tag = replace(tag, "_4_5_", "_4_6_");
	rcs_file->tags[new_tag] = rcs_file->tags[tag];
	m_delete(rcs_file->tags, tag);
      }
    }
  }

  if (git->master_branch >= "5.1") {
    // Help some branches that are on very few files.
    split_branch(rcs_file, "forms2", "2010.12.03.11.56.59");
  }

  // These FCK-related tags from after the 4.5/5.0 split
  // are on both the 4.5 and the 5.0 branches.
  foreach(({ "FCK_DIST", "FCK_2_5_1", "FCK_2_4_3", "FCK_2_4" }), string tag) {
    if (rcs_file->tags[tag]) {
      if (git->master_branch <= "4.5") {
	rcs_file->tags[tag + "/4.5"] = m_delete(rcs_file->tags, tag);
      } else {
	rcs_file->tags[tag + "/5.0"] = m_delete(rcs_file->tags, tag);
      }
    }
  }

  // These are some broken tags on the 4.0 branch.
  foreach(({ "", "__2", "__3", "__4", "__5", "__6", "__7", "__8", "__9",
	     "__10", "__11", "__12", "__13", "__14" }); int i; string suffix) {
    string tag = "sitebuilder_" + suffix;
    if (rcs_file->tags[tag]) {
      rcs_file->tags["sitebuilder_4_0_" + (766 + i)] =
	m_delete(rcs_file->tags, tag);
    }
  }
}

GitRepository.GitCommit filter_commits(GitRepository git,
				       GitRepository.GitCommit commit,
				       RCSFile.Revision rev)
{
  GitRepository.GitCommit master =
    git->git_refs[git->remote + git->master_branch];
  // If the commit is already on our branch, keep it.
  if (master && (commit->leaves & master->is_leaf)) return commit;

  // Special case for commits on real cvs branches.
  if ((rev->revision != "1.1.1.1") &&
      (sizeof(rev->revision/".") > 2)) return commit;

  // If the commit isn't on our parent branch, refuse it.
  master = git->git_refs[git->remote + ([
      "1.3":"1.0",
      "2.0":"1.3",
      "2.1":"2.0",
      "2.2":"2.1",
      "2.3":"2.2",
      "2.4":"2.3",
      "3.3":"2.4",
      "3.4":"3.3",
      "4.0":"3.4",
      "4.5":"4.0",
      "5.0":"4.5",
      "5.1":"5.0",
      "5.2":"5.1",
    ])[real_master_branch]];
  if (master && !(commit->leaves & master->is_leaf)) return UNDEFINED;

  // Force splits at appropriate points in time.
  switch (real_master_branch) {
  case "1.0": break;
  case "1.3":
    // pike-modules/VC.pmod/CVS.pmod/file.pike:1.41
    // Tagged "copied_to_newsitebuilder".
    // 98.10.13.19.37.21
    if (commit->timestamp > 908303841) return UNDEFINED;
    break;
  case "2.0":
    // modules/ac/acadm.pike:1.108
    // 99.09.04.16.23.30
    if (commit->timestamp > 936462210) return UNDEFINED;
    break;
  case "2.1":
    // handlers/xml/common.pike:1.9
    // 2000.04.25.14.24.00
    if (commit->timestamp > 956672640) return UNDEFINED;
    break;
  case "2.2":
    // modules/ac/acimport_osuser.pike:1.56
    // 2000.11.30.20.03.18
    if (commit->timestamp > 975614598) return UNDEFINED;
    break;
  case "2.3":
    // modules/ac/pike-modules/AC.pmod:1.10
    // 2001.09.25.12.33.42
    if (commit->timestamp > 1001421222) return UNDEFINED;
    break;
  case "2.4":
    // pike-modules/Sitebuilder.pmod/FS.pmod/SBCurrentFile.pike:1.11
    // 2002.01.14.12.16.22
    if (commit->timestamp > 1011010582) return UNDEFINED;
    break;
  case "3.3":
    // tabs/files/wizards/commit.pike:1.28
    // 2002.08.19.16.56.18
    if (commit->timestamp > 1029776178) return UNDEFINED;
    break;
  case "3.4":
    // sites/basic/site/cms-common.xsl:1.7
    // 2002.11.18.14.55.45
    if (commit->timestamp > 1037631345) return UNDEFINED;
    break;
  case "4.0":
    // handlers/html/handler.pike:1.34
    // 2003.06.02.16.07.13
    if (commit->timestamp > 1054566433) return UNDEFINED;
    break;
  case "4.5":
    // VERSION:1.862
    // 2004.08.18.11.48.21
    if (commit->timestamp > 1092829701) return UNDEFINED;
    break;
  case "5.0":
    // handlers/html/handler.pike:1.34
    // 2006.10.30.13.33.56
    if (commit->timestamp > 1162215236) return UNDEFINED;
    break;
  case "5.1":
    // pike-modules/Sitebuilder.pmod/Editor.pmod:1.330
    // 2010.11.29.13.34.02
    if (commit->timestamp > 1291037642) return UNDEFINED;
    break;
  case "5.2":
    // modules/manager/insite_editor.pike:1.324
    // 2011.01.18.10.14.21
    if (commit->timestamp > 1295345661) return UNDEFINED;
    break;
  default:
    //if (commit->timestamp > 1029776178) return UNDEFINED;
    break;
  }
  return commit;
}

void rake_leaves(GitRepository git)
{
  // SiteBuilder.
  branch_dependancy(git, "1.0", "1.3", "98.10.17.07.53.54");
  branch_dependancy(git, "1.3", "2.0", "99.09.08.00.00.00");
  branch_dependancy(git, "2.0", "sitebuilder_2.0_freeler",
		    "2000.10.03.01.50.34");
  branch_dependancy(git, "2.0", "2.1", "2000.04.25.00.00.00");
  branch_dependancy(git, "2.1", "sitebuilder_2.1_replicate",
		    "2000.09.18.17.18.46");
  branch_dependancy(git, "2.1", "sitebuilder_2.1_workflow",
		    "2000.11.07.16.01.45");
  branch_dependancy(git, "2.1", "2.2", "2000.09.30.00.00.00");
  branch_dependancy(git, "2.2", "2.3", "2001.09.25.00.00.00");
  branch_dependancy(git, "2.3", "2.4", "2002.01.14.00.00.00");
  branch_dependancy(git, "2.4", "3.3", "2002.08.19.13.49.55");
  branch_dependancy(git, "3.3", "3.4", "2002.11.02.00.00.00");
  branch_dependancy(git, "3.4", "4.0", "2003.06.02.16.07.13");
  branch_dependancy(git, "2.4", "rtl_garbage_free_xslt",
		    "2004.02.02.17.48.33");
  branch_dependancy(git, "3.4", "randstad_ad_work_2005.03",
		    "2005.03.08.15.46.38");
  branch_dependancy(git, "4.0", "randstad_ad_work_2005.11",
		    "2005.11.14.12.58.48");
  branch_dependancy(git, "4.0", "4.5", "2004.08.18.11.48.21");
  branch_dependancy(git, "4.5", "5.0", "2006.10.30.13.33.56");
#if 0	// JOIN_VENDOR_BRANCHES
  branch_dependancy(git, "4.5", "FCK_DIST/4.5", "2008.01.10.08.48.36");
  branch_dependancy(git, "5.0", "FCK_DIST/5.0", "2008.01.10.08.53.56");
#else
  branch_dependancy(git, "4.5", "FCK_DIST/4.5", "2004.10.01.14.36.22");
  branch_dependancy(git, "4.5", "FCK_DIST/5.0", "2004.10.01.14.36.22");
#endif
  branch_dependancy(git, "5.0", "sitebuilder_5.0_workflow",
		    "2008.04.02.16.04.35");
  branch_dependancy(git, "5.0", "5.1", "2010.11.29.00.00.00");
  branch_dependancy(git, "5.1", "forms2", "2010.12.03.11.57.26");
  branch_dependancy(git, "5.1", "5.2", "2011.01.18.10.14.21");

  if (git->git_refs[git->remote + "rtl_garbage_free_xslt"] &&
      git->git_refs["tags/sitebuilder_4.0.605"]) {
    git->git_refs[git->remote + "rtl_garbage_free_xslt"]->
      propagate_dead_leaves(git->git_refs["tags/sitebuilder_4.0.605"]->is_leaf);
  }

  // FIXME: Wouldn't adding these to the set of heads achieve the same?
#if 1 // 0
  GitRepository.Leafset mask =
    git->git_refs[git->remote + "1.3"]->leaves;
  foreach(({ "roxen_1.2.61", "roxen_1.2.72", "roxen_1.2.73",
	     "roxen_1.2.74", "roxen_1.2.75", "forsmark_20000203",
	     "forsmark_20000214", "forsmark_20000218",
	  }), string tag) {
    git_progress(git_flags, "Adjusting tag %O...\n", tag);
    mask |= git->git_refs["tags/" + tag]->leaves;
    foreach(git->git_commits; string c_uuid; GitRepository.GitCommit c) {
      if (!(c->leaves & mask) && !(c->is_leaf) &&
	  !(c->commit_flags & GitRepository.COMMIT_DEAD)) {
	c->propagate_dead_leaves(mask);
      }
    }
  }
#else
  foreach(({ "roxen_1.2.61", "roxen_1.2.72", "roxen_1.2.73",
	     "roxen_1.2.74", "roxen_1.2.75", "forsmark_20000203",
	     "forsmark_20000214", "forsmark_20000218",
	  }), string tag) {
    // These tags act as heads due to sticky-revisions.
    git->heads |= git->git_refs["tags/" + tag]->is_leaf;
  }
#endif
  foreach(git->git_commits; string c_uuid; GitRepository.GitCommit c) {
    if (!has_prefix(c_uuid, "modules/navigation/navigation.pike:1.68."))
      continue;
    c->propagate_dead_leaves(git->heads);
    // mask |= c->leaves;
  }

  git_progress(git_flags, "Attaching some leaves to branches...\n");
  foreach(git->git_refs; string ref; GitRepository.GitCommit c) {
    if (!has_prefix(ref, "tags/sitebuilder_")) continue;
    GitRepository.GitCommit b;
    if (has_prefix(ref, "tags/sitebuilder_3.3.")) {
      b = git->git_refs[git->remote + "3.3"];
    } else if (has_prefix(ref, "tags/sitebuilder_3.4.")) {
      b = git->git_refs[git->remote + "3.4"];
    } else if (has_prefix(ref, "tags/sitebuilder_4.0.")) {
      b = git->git_refs[git->remote + "4.0"];
    } else if (has_prefix(ref, "tags/sitebuilder_4.5.")) {
      b = git->git_refs[git->remote + "4.5"];
    } else continue;
    if (b && !(c->dead_leaves & b->leaves)) {
      b->hook_parent(c);
    }
  }

  if (git->git_refs["tags/tags/ad_work_sync/4.0"] &&
      git->git_refs[git->remote + "4.0"] &&
      !(git->git_refs["tags/tags/ad_work_sync/4.0"]->dead_leaves &
	git->git_refs[git->remote + "4.0"]->leaves)) {
    // Help the ad_work_sync/4.0 tag along.
    git->git_refs[git->remote + "4.0"]->
      hook_parent(git->git_refs["tags/tags/ad_work_sync/4.0"]);
  }
  if (git->git_refs["tags/tags/sitebuilder_3.3.477"] &&
      git->git_refs[git->remote + "5.0"] &&
      !(git->git_refs["tags/tags/sitebuilder_3.3.477"]->dead_leaves &
	git->git_refs[git->remote + "5.0"]->leaves)) {
    // Force the tag to be on the main development branch.
    git->git_refs[git->remote + "5.0"]->
      hook_parent(git->git_refs["tags/tags/sitebuilder_3.3.477"]);
  }
  if (git->git_refs["tags/tags/split_3.3_to_3.4"] &&
      git->git_refs[git->remote + "5.0"] &&
      !(git->git_refs["tags/tags/split_3.3_to_3.4"]->dead_leaves &
	git->git_refs[git->remote + "5.0"]->leaves)) {
    // Force the tag to be on the main development branch.
    git->git_refs[git->remote + "5.0"]->
      hook_parent(git->git_refs["tags/tags/split_3.3_to_3.4"]);
  }
  if (git->git_refs["tags/tags/split_3.3_to_3.4"] &&
      git->git_refs[git->remote + "3.3"] &&
      !(git->git_refs["tags/tags/split_3.3_to_3.4"]->dead_leaves &
	git->git_refs[git->remote + "3.3"]->leaves)) {
    // Force the tag to be on the main development branch.
    git->git_refs[git->remote + "3.3"]->
      hook_parent(git->git_refs["tags/tags/split_3.3_to_3.4"]);
  }
}

void create(GitRepository git, GitFlags flags)
{
  ::create(git, flags);

  string authors_file = combine_path(__FILE__, "../Pike-authors");
  if (Stdio.exist(authors_file)) {
    git->merge_authors(git->read_authors_file(authors_file));
  }

  authors_file = combine_path(__FILE__, "../Roxen-authors");
  if (Stdio.exist(authors_file)) {
    git->merge_authors(git->read_authors_file(authors_file));
  }

  string contributors_file = combine_path(__FILE__, "../SiteBuilder-real-authors");
  if (Stdio.exist(contributors_file)) {
    git->read_contributors_file(contributors_file);
  }
}

